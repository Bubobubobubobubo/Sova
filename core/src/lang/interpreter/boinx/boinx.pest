WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

note_letter = { 'A'..'G' }
letter = _{ ASCII_ALPHA }
digit = _{ ASCII_DIGIT }
int = @{ digit+ }
prev = { "$" }
stop = { "!" }

seq_var = { "§" }
env_func = { "_" }
qualif = _{ seq_var | env_func }

sharp = { "#" }
flat = { "b" }

compo_op = { "|" }
iter_op = { "°" }
each_op = { "~" }

cond_op = _{ "?" }
else_op = _{ ":" }

note = ${ note_letter ~ (sharp | flat)* ~ int? ~ !letter }

real = @{ int ~ "." ~ int }
number = _{ real | int }
micros = ${ int ~ "u" } 
semibeats = ${ number ~ "''" } 
beats = ${ number ~ "'" } 

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
rem = { "-" }
shl = { "<<" }
shr = { ">>" }
pow = { "^" }

bin_op = _{ add | sub | mul | div | rem | shl | shr | pow }

minus = { "-" }
un_op = _{ minus }

lt = { "<" }
le = { "<=" }
eq = { "==" }
ge = { ">=" }
gt = { ">" }
neq = { "!=" }

comp_op = _{ lt | le | eq | ge | gt | neq }

name = { (letter | digit | "_")+ }
ident = ${ !int ~ !note ~ qualif? ~ name }

duration = _{ micros | semibeats | beats }

mute = { "_" }
placeholder = { "." }

sub_prog = { "{" ~ block ~ "}" }
cond = { item ~ comp_op ~ item }
if_else = { cond_op ~ cond ~ sub_prog ~ else_op ~ sub_prog }
el = _{ sub_prog | simul | seque | if_else | placeholder | stop | prev | note | duration | number | ident | mute }

item = _{ un_op? ~ el ~ (bin_op ~ item)? }

seque = { "[" ~ item* ~ "]" }
simul = { "(" ~ item* ~ ")" }

compo = { item ~ ( (compo_op | iter_op | each_op) ~ item )* }

dev = { !":" ~ (int | ident) }
chan = { int | ident }
target = _{ dev ~ (":" ~ chan)? }

output = { compo ~ ("@" ~ target)? }
assign = { ident ~ "=" ~ compo }

statement = _{ assign | output }
block = _{ statement+ }

prog = _{ SOI ~ block? ~ EOI }

/*
Example track:

chords = [ C4 A3 F3 G5 ]

maj = (. .+4 .+7)
min = (. .+3 .+7)

chords ° [ maj min maj maj ] ~ [[. .] . [. .] .] @: 1

chords ~ (. - 12) ~ [....] ° [....] @: 2

kick = 1 snare = 2 hh = 3  

kick | [ . _ ] @: 3
snare | [ _ . ] @: 3
hh | [....] @: 3

after = [_ .] ° [. 1.0]

(pattern delay) ° after

[ (C E G) D [ E E ] F ]

(kick snare) ~ (. hh) ° [ . hh . hh . hh .  hh]

*/
